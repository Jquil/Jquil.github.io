# 前言

因为工作需要，需要写一个VBA程序(Visual Basic for Applications).... 看看介绍：

> VBA代表Visual Basic for Applications，这是一种来自Microsoft的事件驱动编程语言，现在主要与Microsoft Office应用程序(如MSExcel，MS-Word和MS-Access)一起使用。


# Helllo,World

这里我们拿Excel举例来学习：

1。新建一个Excel，另存为`xlsm`格式：启用宏

2。在选项 > 自定义功能区中添加 **开发人员** 选项

3。选中“开发人员”，进入“Visual Basic”，新增一个模组(这是Windows繁体版本下的叫法)，编辑：

```vb
Sub function1()
    MsgBox ("Hello,World")
End Sub
```

4。同样，在“开发人员”选项卡下，新增一个Buttton按钮，并选定巨集(宏)为“function1”

点击试试，是不是出现一个弹窗，内容为：Hello,World呢~


：这里有一个问题，代码没提示呀~

这样我们用VSCode来编辑代码后，再贴到Excel中去测试，我们需要安装以下插件：

<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/20211106005.png)
</div>

然后新建一个文件，格式为：**cls**，编辑的时候就会代码提示拉~


# 声明变量


VBA中的数据类型有以下：

<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/201611140928061888.png)
</div>


声明变量使用“Dim”关键字

```vb
Sub function1()

    Dim flag As Boolean
    flag = true

    Dim num As Integer
    num = 1

    Dim name As String
    name = "Jq8686"

    MsgBox(flag)
    MsgBox(num)
    MsgBox(name)
End Sub
```

# 流程控制

### 选择结构

我们来看以下代码：

```vb
Sub function1()

    Dim num As Integer
    num = 1

    If num = 1 Then
        MsgBox "this is 1"
    ElseIf num = 2 Then
        MsgBox "this is 2"
    Else
        MsgBox "..."
    End If

End Sub
```

1。它是 `if-then-elseif-then-else-end if`的格式

2。**判断两个值是否相等是用一个等号**


我们再来用Select实现：
```vb
Sub function1()

    Dim num As Integer
    num = 1

    Select Case num
        Case 1
            MsgBox "this is 1"
        Case 2
            MsgBox "this is 2"
        Case else
            MsgBox "..."
    End Select

End Sub
```

1。它是以“select case {变量}” 形式

2。“case else”来判断其他的情况


到目前为止，有没有发现代码都是对称的：

1. sub - end sub

2. if - end if

3. select - end select

而且代码中是没有“:”，“;”，“{}” 这类的符号的，这或许就是这门语言的特点把~

### 循环结构

```vb
Sub function1()

    for i = 0 to 10 step 2
        MsgBox "this is " & i
    next

End Sub
```

不难理解，不过字符串的拼接符号变成了 “&”

再来试试while：
```vb
Sub function1()

    Dim i As Integer

    While i <= 10
        MsgBox "this is " & i
        i = i + 2
    Wend
    
End Sub
```

1。`while-wend`形式

2。试了一下，不支持`i+=2`，也不支持三目运算

3。不等于是以`<>`形式

# 字符串处理

在实际需求中，我们会需要对字符串进行截取，分割，获取特定字符的位置等等，那么来看看在VBA中是如何实现的吧

```vb
Sub function1()

    Dim str As String
    str = "Jq8686"

    ' 字符串替换 Jq6666 => Jq8686
    MsgBox Replace(str,"8686","6666")

    ' 输出：1，返回对应子串的起始位置（从1开始）
    MsgBox InStr(1,str,"Jq")

    ' 输出：3
    MsgBox InStr(1,str,"8686")

    ' UBound() 是用来获取一个数组的上界，+1就是它的实际长度
    Dim say As String
    say = "she is so beautiful"
    Dim arr() as String
    arr = Split(say," ")
    MsgBox "Length = " & UBound(arr) + 1
End Sub
```


关于更多字符串的函数请看：[详情](https://docs.microsoft.com/zh-cn/dotnet/visual-basic/language-reference/functions/string-functions)

# 数组

在字符串处理中我们已经有使用到了数组，关于数组的使用，经常会遍历

```vb
Sub function1()

    ' 1
    Dim arr1(4)
    arr1(0) = "Jq8686"
    arr1(1) = True
    arr1(2) = 20
    arr1(3) = 10.5
    For Each Item In arr1
        MsgBox Item
    Next

    ' 2
    arr2 = Array("Jq8686", "男", "计算机")
    For i = 0 To UBound(arr2)
        MsgBox arr2(i)
    Next

    ' 3
    Dim arr3() As String
    ReDim arr3(2)
    arr3(0) = "Jq1"
    arr3(1) = "Jq2"
    MsgBox arr3(0) & " - " & arr3(1)
    ReDim arr3(2)
    MsgBox arr3(0) & " - " & arr3(1)
    arr3(0) = "Jq1"
    arr3(1) = "Jq2"
    ReDim Preserve arr3(3)
    arr3(2) = "Jq3"
    MsgBox arr3(0) & " - " & arr3(1) & " - " & arr3(2)

    
End Sub
```

上面介绍了数组的三种创建方式与遍历方式，我们主要来看第三种，第三种是实现不定长数组

1. `ReDim` 重新定义

2. `ReDim Preserve` 重新定义，并保留原有值


# 操作单元格

> 这里举两个例子：检查，抽签。

## 检查

### 简单计算
首先是这样的一个效果：


<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/20211106006.gif)
</div>

那么我们的程序应该是这样的
```vb
Sub function1()
    Dim sheet1   As Worksheet
    Dim str      As String
    Dim signPos  As Integer
    Dim leftVal  As Double
    Dim rightVal As Double
    Set sheet1 = Worksheets("工作表1")
    str        = sheet1.Range("A2")
    signPos    = Instr(str,"±")
    leftVal    = Mid(str,1,signPos - 1)
    rightVal   = Mid(str,signPos + 1)

    sheet1.Range("B2").Value = leftVal + rightVal
    sheet1.Range("C2").Value = leftVal - rightVal
    
End Sub
```


很简单，看看代码~

1。`Worksheets("工作表1").Range("A2")` 获取工作表1的A2单元格

2。`Instr` 获取子串的位置

3。`Mid` 截取字符串

### 增加干扰


<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/20211106007.png)
</div>


可以看到多出了很多杂项进行干扰：

1。`5-35.4+0.3/-0` 的“5-”这个前缀是不需要的

2。`FAI46: 1.831±0.04` 的“FAI46: ”是不需要的

3。`3.675+0.05/-0.03` 加减的数可能不同


<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/20211106008.gif)
</div>

代码如下：

```vb
Sub function1()

    Dim sheet1   As Worksheet
    Dim forSize  As Integer     ' 需要遍历的数量
    Dim str      As String      ' 单元格内容
    Dim leftVal  As Double      ' 左值
    Dim rightVal As Double      ' 右值

    Set sheet1 = Worksheets("工作表1")
    
    ' 计算出需要遍历的数量
    forSize = 100
    Dim flagFor As Boolean
    flagFor = True
    while flagFor
        If sheet1.Range("A"&forSize).Value <> "" Then
            forSize = forSize + forSize
        else
            flagFor = false
        End If
    wend


    ' 遍历
    Dim subIndex As Integer 
    for i = 2 to forSize step 1
        str = sheet1.Range("A"&i)
        If str<>"" Then
            
            ' 去掉所有空格
            str = Replace(str," ","")

            ' 删除掉前缀
            subIndex = getSubIndex(str)
            If subIndex <> 0 Then
                str = Mid(str,subIndex + 1)
            End If

            ' 去掉无用的符号
            str = Replace(str,"Ф","")

            ' 计算
            Dim signPos1  As Integer    ' ±
            Dim signPos2  As Integer    ' +
            Dim signPos3  As Integer    ' -
            signPos1 = Instr(str,"±")
            signPos2 = Instr(str,"+")
            signPos3 = Instr(str,"-")
            ' 分类
            If signPos1 <> 0 Then
                leftVal  = Mid(str,1,signPos1 - 1)
                rightVal = Mid(str,signPos1 + 1)
                sheet1.Range("B"&i).Value = leftVal + rightVal
                sheet1.Range("C"&i).Value = leftVal - rightVal
            elseIf (signPos2 <> 0 And signPos3 <> 0) Then
                leftVal  = Mid(str,1,signPos2 - 1)
                sheet1.Range("B"&i).Value = leftVal + Mid(str,signPos2 + 1,Instr(str,"/") - Instr(str,"+") - 1)
                sheet1.Range("C"&i).Value = leftVal - Mid(str, InStr(str, "/") + 2)
            End If

        End If
    next
    
End Sub


' 用于获取无用前缀的下标
Function getSubIndex(str As String) As Integer
    Dim horPos      As Integer   ' -
    Dim colonPos    As Integer   ' ：
    getSubIndex = 0
    
    horPos      = Instr(str,"-")
    colonPos    = Instr(str,":")
    
    ' 5-35.4+0.3/-0 => 35.4+0.3/-0
    If horPos <> 0 Then
        If StrComp(Mid(str, horPos - 1, 1), "/") <> 0 Then
            getSubIndex = horPos
        End If
    End If

    ' FAI6: 3.675+0.05/-0.03 => 3.675+0.05/-0.03
    If colonPos <> 0 Then
        getSubIndex = colonPos
    End If

End Function
```

### 检查

现在再来实现：数值已经有员工手动计算好了，我们需要对它进行检查

<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/20211106009.gif)
</div>

```vb
Sub function1()

    ' 遍历
    Dim subIndex As Integer
    Dim maxRes   As Double
    Dim minRes   As Double
    Dim errNum   As Integer
    Dim lColor   As Integer
    Dim rColor   As Integer
    for i = 2 to forSize step 1
        lColor = 0
        rColor = 0
        str = Worksheets("工作表1").Range("A"&i)
        If str<>"" Then
            ...
            ' 分类
            ...

            ' 计算出最大最小值
            If signPos1 <> 0 Then
                leftVal = Mid(str, 1, signPos1 - 1)
                rightVal = Mid(str, signPos1 + 1)
                maxRes = leftVal + rightVal
                minRes = leftVal - rightVal
            ElseIf (signPos2 <> 0 And signPos3 <> 0) Then
                leftVal = Mid(str, 1, signPos2 - 1)
                maxRes = leftVal + Mid(str, signPos2 + 1, InStr(str, "/") - InStr(str, "+") - 1)
                minRes = leftVal - Mid(str, InStr(str, "/") + 2)
            End If

            ' 进行比对并进行样式填充
            If Worksheets("工作表1").Range("B"&i).Value <> maxRes Then
                errNum = errNum + 1
                lColor = 6
            End If
            If Worksheets("工作表1").Range("C"&i).Value <> minRes Then
                errNum = errNum + 1
                rColor = 6
            End If
            Worksheets("工作表1").Range("B"&i).Interior.ColorIndex = lColor
            Worksheets("工作表1").Range("C"&i).Interior.ColorIndex = rColor
        End If
    next

    MsgBox "error number = " & errNum

End Sub
```

## 抽签

我们先来看看实现的效果：

<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/202111060010.gif)
</div>

```vb
Public Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal Milliseconds As LongPtr)
Sub function1()
    Dim colorIndex  as Integer
    Dim selectIndex as Integer
    Dim min         as Integer
    Dim max         as Integer
    min = 2
    max = 6
    colorIndex  = 6
    selectIndex = (max - min) * Rnd() + min
    Range("A"&selectIndex).Interior.ColorIndex = 0

    For i = 0 to 5
        For j = min to max Step 1
            Range("A"&j).Interior.ColorIndex = colorIndex
            If (i = 5 and j = selectIndex) Then
                Exit For
            End If
            Sleep 100
            Range("A"&j).Interior.ColorIndex = 0
        Next
    Next
End Sub
```


注意：跳出循环不再是`break`了，而是`exit for`

但这样子感觉比较单调，我们来加上个权重，权重越大，抽中概率越大：

```vb
Public Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal Milliseconds As LongPtr)

Type MaxObj
    index as Integer
    val   as Integer
End Type

Sub function1()
    Dim colorIndex  as Integer
    Dim selectIndex as Integer
    Dim mFrom       as Integer
    Dim mTo         as Integer
    Dim max         as Integer
    Dim maxIndex    as Integer
    Dim arr()       as MaxObj
    colorIndex  = 6
    maxIndex    = 0
    max         = 0
    mFrom       = 2
    mTo         = 6
    
    ' 获取到最大的权重
    For i = mFrom to mTo step 1
        Range("A"&i).Interior.ColorIndex = 0
        if Range("B"&i) > max Then
            max = Range("B"&i)
            maxIndex = i
        end if
    Next

    Redim arr(1)
    With arr(1)
        .index = maxIndex
        .val   = max
    end With


    ' 取出相同的最大权重并加入到数组
    For i = mFrom to mTo step 1
        if (Range("B"&i) = max and i <> maxIndex) Then
            
            ReDim Preserve arr(UBound(arr)+1)
            With arr(UBound(arr))
                .index = i
                .val   = Range("B"&i)
            end With
        end if
    Next

    selectIndex = (UBound(arr) - 1) * Rnd() + 1
    selectIndex = arr(selectIndex).index

    For i = 0 to 5
        For j = mFrom to mTo Step 1
            Range("A"&j).Interior.ColorIndex = colorIndex
            If (i = 5 and j = selectIndex) Then
                Exit For
            End If
            Sleep 100
            Range("A"&j).Interior.ColorIndex = 0
        Next
    Next
End Sub
```

实现效果如下：

<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/2021110613.gif)
</div>


再加一个功能好了：已经抽过了的就不再抽了

其实很简单，只需要加上：`Range("C"&i) <> 1`的判断就可以了~

最终效果如下：
<div align='center'>

![](https://jqwong.cn/file/markdown/note/141/img/2021110614.gif)
</div>


# 小结


这里贴上几个关于VBA的教程：

1. [易百教程：VBA](https://www.yiibai.com/vba/vba_switch_statement.html)

2. [EXCEL VBA 入门到精通详细教程](https://www.lanrenexcel.com/excel-vba-tutorial/)

关于VBA入门知识先学习到这了~